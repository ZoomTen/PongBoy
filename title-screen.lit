@title Adding a title screen
@overwrite_css style.css
@comment_type ; %s

@s Setting up a new game mode

<figure>
<img src="../figures/game-mode.svg">
</figure>

Well, it's time to use this new system to add a new screen: the title screen. At the moment, it really should just do nothing but say "Press Start", and then kicks you into straight into the game. Although from the player's perspective it comes before the gameplay, internally I'm going to define this mode *after* the gameplay mode.

Because its only job is to show the game's title and wait for joypad input, the code is gonna be really simple:

--- GM_Title init code
GM_Title_init::
	call DisableLCD
@{Set up the title screen graphics}
	jp EnableLCD
---

--- GM_Title loop code
GM_Title_loop::
@{Handle title screen joypad input}
	ret
---

@s Initialization

Of course, the initialization is simply just copying the necessary graphics data to VRAM...

--- Set up the title screen graphics
	ld hl, vChars0
	ld de, TitleScreenGFX
	ld bc, TitleScreenGFX_END - TitleScreenGFX
	call CopyMem16
	
	ld hl, vBGMap0
	ld de, TitleScreenMAP
	ld bc, TitleScreenMAP_END - TitleScreenMAP
	call CopyMem16
---

And both the tileset and the tilemap are generated off of a single png...

<figure>
<img class="pixelated" alt="Game graphics" src="../src/gfx/title-screen.png">
<figcaption>Title screen graphics. Yes, I just made an entire 256&times;256 px tile map for only one screenful of graphics, 'cuz I'm just lazy.</figcaption>
</figure>

--- Miscellaneous data +=
TitleScreenGFX::
	incbin "gfx/title-screen.2bpp"
TitleScreenGFX_END::
---

--- Miscellaneous data +=
TitleScreenMAP::
	incbin "gfx/title-screen.map"
TitleScreenMAP_END::
---

@s Loop

The loop is just checking whether or not the START button is pressed, with an early `ret` if not:

--- Handle title screen joypad input
	call ReadJoypad
	ldh a, [hInput]
	bit BUTTONF_START, a
	ret z
---

But if it *is* pressed, it'll jump to the game.

--- Handle title screen joypad input +=
	xor a  ; ld a, GM_GAME
	ldh [hGameMode], a
	ret
---

@s Adding the title screen

Let's add all of the stuff above into the ROM.

--- Game mode initialization code +=
@{GM_Title init code}
---

--- Game mode loop code +=
@{GM_Title loop code}
---

--- Pointers to game mode initialization routines +=
	dw GM_Title_init
---

--- Pointers to game mode loop routines +=
	dw GM_Title_loop
---

@s Booting up to the title screen

I'll change the starting game mode here.

--- Game mode constants +=
GM_TITLE equ 1
---

--- Set the initial game mode
	ld a, $ff
	ldh [hOldGameMode], a
	ld a, GM_TITLE ; +++
	ldh [hGameMode], a
---


